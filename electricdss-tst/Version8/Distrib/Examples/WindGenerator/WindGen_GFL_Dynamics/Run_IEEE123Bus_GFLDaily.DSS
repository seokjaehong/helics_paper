// Edited on DSS-Extensions to reuse the original IEEE 123 data, and add required
// changes in this file, instead of copying every single file.
//
// Why was this edited? To avoid duplication an simplify maintenance.
//
// Known differences:
// - The original LineCode.4 in this folder wasn't updated and wrong elements in `CMatrix`.

! Annotated "Run" file for the IEEE 123 Bus Test Case
! Modified for demonstrating the grid forming and grid following capabilites
! added to OpenDSS on 10/28/2022

Redirect (../../../IEEETestCases/123Bus/IEEE123Master.dss)
BatchEdit Load..* Daily=default
Set VoltageBases = [4.16, 0.69, 0.48]    ! ARRAY OF VOLTAGES IN KV
BusCoords (../../../IEEETestCases/123Bus/BusCoords.dat)   ! load in bus coordinates

! 'Compile' differs from "redirect" in that it changes the default directory 
! to the one in which the referenced file is located.
! 'Redirect' returns to the directory from which it was invoked.

! After compiling, the next series of script commands modify to the regulator control definitions to
! limit the maximum tap change to 1 per solution control iteration. In most cases, this will 
! allow the standard DSS solution algorithm to better represent an actual control.
! Note the time delay is also chnaged. This forces the regulator at the head of the feeder to move first.

RegControl.creg1a.maxtapchange=1  Delay=15  !Allow only one tap change per solution. This one moves first
RegControl.creg2a.maxtapchange=1  Delay=30  !Allow only one tap change per solution
RegControl.creg3a.maxtapchange=1  Delay=30  !Allow only one tap change per solution
RegControl.creg4a.maxtapchange=1  Delay=30  !Allow only one tap change per solution
RegControl.creg3c.maxtapchange=1  Delay=30  !Allow only one tap change per solution
RegControl.creg4b.maxtapchange=1  Delay=30  !Allow only one tap change per solution
RegControl.creg4c.maxtapchange=1  Delay=30  !Allow only one tap change per solution

! The default maximum control iterations is 10. With so many regulators each moving only one tap at a time,
! this is insufficient for the 123-bus test case, which will need 15 control iterations for the initial solution.
! Therefore, the 'MaxControlIter' option is set to 30 to be safe.

Set MaxControlIter=30

! Solve executes the solution for the present solution mode, which is "snapshot".

New EnergyMeter.Feeder Line.L115 1

! This case considers the default load shape to represent the load demand in time for daily simulations
! Adding DER 

new Transformer.TWindG phases=3 windings=2 buses=(WindGB 101) conns=(wye delta) kvs=(0.69 4.16) kvas=(1000 1000) taps='1 1' XHL=0.5

! Adding variables, references and object for WindGen

New XYcurve.PLosses npts=19 
~ yarray=[43.7934 25.71 16.5371 11.45 8.422 6.5199 5.2913 4.4653 3.37 2.34 1.31 0.289 0.28 0.28 0.28 0.28 0.28 0.28 0.28] 
~ xarray=[5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23]

New XYcurve.generic npts=4 yarray=[0.44 0 0 -0.44] xarray=[0.95 0.98 1.02 1.05]

New WindGen.wtg1 phases=3 bus1=WindGB kV=0.69 kVA=1200.0         
~ model=1 Xthev=0.05 Vss=1.0 Pss=0.8 Qss=0.0 vwind=10
~ N_WTG=1 SimMechFlg=1 APCFLG=1 delt0=0.0001 DebugTrace=0 VV_Curve=generic QMode=2


! Monitors

new monitor.WindGen   element=WindGen.wtg1 terminal=1 mode=3
new monitor.WindGenS  element=WindGen.wtg1 terminal=1 mode=1
new monitor.WindGenPQ element=WindGen.wtg1 terminal=1 mode=1 ppolar=No

calcv
solve

! Show some selected results

plot profile phases=all

! Simulation starts at 10 hours, then there is a fault in the system
! Starts in dynamics mode and goes for 200 ms

set mode=dynamics stepsize=0.001 maxiterations=500 number=200

set time=(0,0)
solve

! Then the fault happens at bus 57
New Fault.myFault phases=3 bus1=51

set number=70
solve
! The fault is cleared
Fault.myFault.enabled=False
! Goes for another 2 s to bring the system back
set number=2000
solve

! Export monitors
export monitors all




